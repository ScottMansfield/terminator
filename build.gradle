apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

group = 'com.widowcrawler'
archivesBaseName = 'terminator'
version = '0.1.1'

repositories {
    mavenCentral()
}

dependencies {
    // Utils
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.4'

    // For pulling the sitemaps
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile 'org.glassfish.jersey.core:jersey-client:2.17'

    // logging
    compile 'org.slf4j:slf4j-api:1.7.12'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testRuntime 'org.slf4j:slf4j-simple:1.7.12'
}

// Publishing bits
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Terminator'
                packaging 'jar'
                description 'Terminator is a parser for robots.txt files.'
                url 'https://github.com/ScottMansfield/terminator'

                scm {
                    url 'https://github.com/ScottMansfield/terminator'
                    connection 'scm:git@github.com:ScottMansfield/terminator.git'
                    developerConnection 'scm:git@github.com:ScottMansfield/terminator.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ScottMansfield'
                        name 'Scott Mansfield'
                        email 'sgmansf@gmail.com'
                    }
                }
            }
        }
    }
}
